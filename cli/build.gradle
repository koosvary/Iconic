apply plugin: 'application'

mainClassName = 'org.iconic.Client'
applicationDefaultJvmArgs = ["-ea"]

run {
  if (project.hasProperty("appArgs")) {
    args Eval.me(appArgs)
  }
}

dependencies {
  compile project(':api')
  compile group: 'com.beust', name: 'jcommander', version: '1.72'
  compile group: 'org.knowm.xchart', name: 'xchart', version: '3.5.2'
  compile group: 'org.apache.commons', name: 'commons-csv', version: '1.6'
}

jar {
  baseName = 'iconic-cli'
  version = '0.7.1'

  // The Java plugin doesn't specify any of the 
  // manifest attributes, so we need at least
  // these two attributes for the JAR to work
  manifest {
    attributes(
      'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
      'Main-Class': 'org.iconic.Client'
    )
  }

  from {
    configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  }
}
